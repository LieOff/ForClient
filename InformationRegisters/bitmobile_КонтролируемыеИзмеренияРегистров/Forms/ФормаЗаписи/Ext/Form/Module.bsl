
/////////////////////////////////////////////////////////////////////////////////////////////////
// Общие процедуры и функции

&НаСервере
Функция ПолучитьСписокОбъектов(НачальноеЗначение)
	
	ТекущийСписок = Новый СписокЗначений;
	
	Если ЗначениеЗаполнено(НачальноеЗначение) Тогда 
		ТекущийСписок.Добавить(НачальноеЗначение);
	КонецЕсли;
	
	// Прочитать список регистров сведений
	Для Каждого СтрокаРегистр ИЗ Метаданные.РегистрыСведений Цикл
		
		ПолноеИмя 	= СтрокаРегистр.ПолноеИмя();
		Имя 		= СтрокаРегистр.Имя;
		
		Если Не Имя = "bitmobile_СоответствиеПеречислений"
		   И Не Имя = "bitmobile_ИзмененныеДанные" 
		   И Не Имя = "bitmobile_КонтролируемыеИзмеренияРегистров" 
		   И Не ПолноеИмя = НачальноеЗначение Тогда
			
			ТекущийСписок.Добавить(ПолноеИмя);
			
		КонецЕсли;
					
	КонецЦикла;
	   
	// Прочитать список регистров накопления
	Для Каждого СтрокаРегистр ИЗ Метаданные.РегистрыНакопления Цикл 
		
		ПолноеИмя = СтрокаРегистр.ПолноеИмя();
		
		Если Не ПолноеИмя = НачальноеЗначение Тогда

			ТекущийСписок.Добавить(ПолноеИмя);
			
		КонецЕсли;
										
	КонецЦикла;
		
	Возврат ТекущийСписок;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокИзмерений()
	
	ТекущийСписок = Новый СписокЗначений;
	
	// Получить таблицу актуальных настроек
	Запрос = Новый Запрос;
	Запрос.Текст = 
	    "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	    |	bitmobile_НастройкиСинхронизации.Ссылка,
	    |	bitmobile_НастройкиСинхронизации.ВидОбъектаBitmobile,
	    |	bitmobile_НастройкиСинхронизации.ОбъектКонфигурации,
	    |	bitmobile_НастройкиСинхронизации.Наименование
	    |ИЗ
	    |	Справочник.bitmobile_НастройкиСинхронизации КАК bitmobile_НастройкиСинхронизации
	    |ГДЕ
	    |	bitmobile_НастройкиСинхронизации.ПометкаУдаления = ЛОЖЬ
	    |	И bitmobile_НастройкиСинхронизации.ВыгрузкаДанных = ИСТИНА";
		
	ТаблицаНастроек = Запрос.Выполнить().Выгрузить();
	
	МетаданныеРегистра = Метаданные.НайтиПоПолномуИмени(Запись.ИмяРегистра);
	
	Для Каждого ИзмерениеРегистра Из МетаданныеРегистра.Измерения Цикл
		
		СписокТипов = ИзмерениеРегистра.Тип.Типы();
		
		ДобавитьИзмерение = Ложь;
		
		Для Каждого ТипИзмерения Из СписокТипов Цикл 
			
			МетаданныеТипа = Метаданные.НайтиПоТипу(ТипИзмерения);
			
			Если Не МетаданныеТипа = Неопределено Тогда 
			
				ПолноеИмяМетаданного = МетаданныеТипа.ПолноеИмя();
				
				НайденнаяНастройка = ТаблицаНастроек.Найти(ПолноеИмяМетаданного, "ОбъектКонфигурации");
				
				Если Не НайденнаяНастройка = Неопределено Тогда 
					
					ДобавитьИзмерение = Истина;
					
				КонецЕсли;
								
			КонецЕсли;
						
		КонецЦикла;
		
		Если ДобавитьИзмерение Тогда 
			
			ТекущийСписок.Добавить(ИзмерениеРегистра.Имя);
			
		КонецЕсли;
				
	КонецЦикла;
	
	Возврат ТекущийСписок;	
	
КонецФункции	

/////////////////////////////////////////////////////////////////////////////////////////////////
// Командный интерфейс

&НаКлиенте
Процедура ИмяРегистраНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокОбъектов = ПолучитьСписокОбъектов(Элемент.ТекстРедактирования);
	
	ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ИмяРегистраНачалоВыбораЗавершение", ЭтаФорма)
						 , СписокОбъектов
						 , Элемент
						 , );
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяРегистраНачалоВыбораЗавершение(Результат, Параметр) Экспорт
	
	Выбор = Результат;
	
	Если НЕ Выбор = Неопределено И ЗначениеЗаполнено(Выбор.Значение) Тогда 
		
		Если НЕ Запись.ИмяРегистра = Выбор.Значение Тогда 
			
			Запись.ИмяРегистра = Выбор.Значение;
			Запись.ИмяИзмерения = "";
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяИзмеренияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущийСписок = ПолучитьСписокИзмерений();
	
	ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ИмяИзмеренияНачалоВыбораЗавершение", ЭтаФорма)
						 , ТекущийСписок
						 , Элемент
						 , );
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяИзмеренияНачалоВыбораЗавершение(Результат, Параметр) Экспорт
	
	Выбор = Результат;
	
	Если НЕ Выбор = Неопределено И ЗначениеЗаполнено(Выбор.Значение) Тогда 
		
		Запись.ИмяИзмерения = Выбор.Значение;
		
	КонецЕсли;
	
КонецПроцедуры