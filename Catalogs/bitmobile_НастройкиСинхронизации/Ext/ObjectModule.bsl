
Процедура ПриЗаписи(Отказ)
	
	Если Не ПометкаУдаления Тогда 
		
		Если Не Отказ Тогда 	
			
			// Получить таблицу текущих настроек
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	bitmobile_НастройкиСинхронизации.Ссылка КАК Ссылка,
				|	bitmobile_НастройкиСинхронизации.ОбъектКонфигурации КАК ОбъектКонфигурации
				|ИЗ
				|	Справочник.bitmobile_НастройкиСинхронизации КАК bitmobile_НастройкиСинхронизации
				|ГДЕ
				|	bitmobile_НастройкиСинхронизации.ПометкаУдаления = ЛОЖЬ
				|	И bitmobile_НастройкиСинхронизации.ВыгрузкаДанных = ИСТИНА";

			ТаблицаТекущихНастроек = Запрос.Выполнить().Выгрузить();

			Если Не ОбменДанными.Загрузка Тогда
			
				//////////////////////////////////////////////////////////
				// Перезаписать регистр bitmobile_СоответствиеПеречислений
				
				// Получить копию регистра перечислений
				Запрос = Новый Запрос;
				Запрос.Текст = 
					"ВЫБРАТЬ РАЗРЕШЕННЫЕ
					|	bitmobile_СоответствиеПеречислений.ИмяВКонфигурации,
					|	bitmobile_СоответствиеПеречислений.ИмяВBitmobile,
					|	bitmobile_СоответствиеПеречислений.СоответствиеЗначенийПеречисления
					|ИЗ
					|	РегистрСведений.bitmobile_СоответствиеПеречислений КАК bitmobile_СоответствиеПеречислений";

				КопияРегистра = Запрос.Выполнить().Выгрузить();

				// Очистить регистр перечислений
				НаборЗаписей = РегистрыСведений.bitmobile_СоответствиеПеречислений.СоздатьНаборЗаписей();
				НаборЗаписей.Записать();

				ТаблицаЗаписей = КопияРегистра.Скопировать();
				ТаблицаЗаписей.Очистить();
				
				Для Каждого СтрокаНастройки Из ТаблицаТекущихНастроек Цикл 
				
					МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(СтрокаНастройки.Ссылка.ОбъектКонфигурации);
					
					// Обработать реквизиты шапки объекта
					Для Каждого СтрокаРеквизита Из СтрокаНастройки.Ссылка.РеквизитыШапки Цикл
						
						Если ЗначениеЗаполнено(СтрокаРеквизита.РеквизитОбъектаBitmobile) И Не СтрокаРеквизита.СтандартныйРеквизит И Не СтрокаРеквизита.ПриводитьКСтроке Тогда 
							
							Попытка
								
								ТипРеквизита = МетаданныеОбъекта.Реквизиты[СтрокаРеквизита.РеквизитОбъектаКонфигурации].Тип.Типы().Получить(0);
								
								ПроверитьИЗаписатьПеречисление(ТипРеквизита, КопияРегистра, ТаблицаЗаписей);
								
							Исключение
								
								Сообщение = Новый СообщениеПользователю;
								Сообщение.Текст = "В настройке """ + СтрокаНастройки.ОбъектКонфигурации + """ не найден реквизит шапки """ + СтрокаРеквизита.РеквизитОбъектаКонфигурации + """. Сообщение об ошибке: """ + ОписаниеОшибки() + """";
								Сообщение.Сообщить();
								
							КонецПопытки;
							
						КонецЕсли;
						
					КонецЦикла;
					
					// Обработать реквизиты табличных частей объекта
					Для Каждого СтрокаТЧ Из СтрокаНастройки.Ссылка.ТабличныеЧасти Цикл
						
						Если Не СтрокаТЧ.Запрос Тогда
							
							НайденныеРеквизиты = СтрокаНастройки.Ссылка.РеквизитыТабличныхЧастей.НайтиСтроки(Новый Структура("ТабличнаяЧастьОбъектаКонфигурации", СтрокаТЧ.ТабличнаяЧастьОбъектаКонфигурации));
							
							Для Каждого СтрокаРеквизита Из НайденныеРеквизиты Цикл
							
								Если ЗначениеЗаполнено(СтрокаРеквизита.РеквизитОбъектаBitmobile) И Не СтрокаРеквизита.СтандартныйРеквизит И Не СтрокаРеквизита.ПриводитьКСтроке Тогда 
									
									Попытка
										
										ТипРеквизита = МетаданныеОбъекта.ТабличныеЧасти[СтрокаРеквизита.ТабличнаяЧастьОбъектаКонфигурации].Реквизиты[СтрокаРеквизита.РеквизитОбъектаКонфигурации].Тип.Типы().Получить(0);
										
										ПроверитьИЗаписатьПеречисление(ТипРеквизита, КопияРегистра, ТаблицаЗаписей);
										
									Исключение
										
										Сообщение = Новый СообщениеПользователю;
										Сообщение.Текст = "В настройке """ + СтрокаНастройки.ОбъектКонфигурации + """ не найден реквизит табличной части """ + СтрокаРеквизита.ТабличнаяЧастьОбъектаКонфигурации + "." + СтрокаРеквизита.РеквизитОбъектаКонфигурации + """. Сообщение об ошибке: """ + ОписаниеОшибки() + """";
										Сообщение.Сообщить();
										
									КонецПопытки;
									
								КонецЕсли;
								
							КонецЦикла;
							
						Иначе 
							
							Если ЗначениеЗаполнено(СтрокаТЧ.ТекстЗапроса) Тогда 
								
								НайденныеРеквизиты = СтрокаНастройки.Ссылка.РеквизитыТабличныхЧастей.НайтиСтроки(Новый Структура("ТабличнаяЧастьОбъектаКонфигурации", СтрокаТЧ.ТабличнаяЧастьОбъектаКонфигурации));
								
								Попытка
									
									Построитель = Новый ПостроительЗапроса;
									Построитель.Текст = СтрокаТЧ.ТекстЗапроса;
									Построитель.ЗаполнитьНастройки();
									
									Для Каждого СтрокаРеквизита Из НайденныеРеквизиты Цикл
										
										Если СтрокаРеквизита.ПриводитьКСтроке Тогда 
											
											Продолжить;
											
										КонецЕсли;
										
										ПолеТипа = Построитель.ДоступныеПоля.Найти(СтрокаРеквизита.РеквизитОбъектаКонфигурации);
										
										ТипРеквизита = ПолеТипа.ТипЗначения.Типы().Получить(0);
										
										ПроверитьИЗаписатьПеречисление(ТипРеквизита, КопияРегистра, ТаблицаЗаписей);
										
									КонецЦикла;
									
								Исключение
									
									Сообщение = Новый СообщениеПользователю;
									Сообщение.Текст = "Обнаружен некорректный запрос в настройке """ + СтрокаНастройки.ОбъектКонфигурации + """. Сообщение об ошибке: """ + ОписаниеОшибки() + """";
									Сообщение.Сообщить();
									
								КонецПопытки;
								
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЦикла;
				
				ТаблицаЗаписей.Свернуть("ИмяВКонфигурации, ИмяВBitmobile, СоответствиеЗначенийПеречисления");
				
				НовыйНаборЗаписей = РегистрыСведений.bitmobile_СоответствиеПеречислений.СоздатьНаборЗаписей();
				НовыйНаборЗаписей.Загрузить(ТаблицаЗаписей);
				НовыйНаборЗаписей.Записать();
				
			КонецЕсли;	
			
			//////////////////////////////////////////////////////////////////
			// Перезаписать регистр bitmobile_КонтролируемыеИзмеренияРегистров
			
			// Получить копию регистра регистра контролируемых измерений
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	bitmobile_КонтролируемыеИзмеренияРегистров.ИмяРегистра,
				|	bitmobile_КонтролируемыеИзмеренияРегистров.ИмяИзмерения
				|ИЗ
				|	РегистрСведений.bitmobile_КонтролируемыеИзмеренияРегистров КАК bitmobile_КонтролируемыеИзмеренияРегистров";
			
			КопияРегистраИзмерений = Запрос.Выполнить().Выгрузить();
			
			// Очистить регистр измерений
			НаборЗаписейИзмерений = РегистрыСведений.bitmobile_КонтролируемыеИзмеренияРегистров.СоздатьНаборЗаписей();
			НаборЗаписейИзмерений.Записать();
			
			ТаблицаИзмерений = РегистрыСведений.bitmobile_КонтролируемыеИзмеренияРегистров.СоздатьНаборЗаписей().Выгрузить();
			
			Для Каждого СтрокаКопии Из КопияРегистраИзмерений Цикл 
				
				ОбработатьИзмерение = Истина;
				
				Попытка
					
					МетаданныеРегистра = Метаданные.НайтиПоПолномуИмени(СтрокаКопии.ИмяРегистра);
					
					СписокТипов = МетаданныеРегистра.Измерения[СтрокаКопии.ИмяИзмерения].Тип.Типы();
					
				Исключение
					
					ОбработатьИзмерение = Ложь;
					
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = "Обнаружено некорректное контролируемое измерение """ + СтрокаКопии.ИмяРегистра + "." + СтрокаКопии.ИмяИзмерения + """. Запись удалена. Сообщение об ошибке: """ + ОписаниеОшибки() + """";
					Сообщение.Сообщить();
					
				КонецПопытки;
				
				Если ОбработатьИзмерение Тогда
				
					ДобавитьИзмерение = Ложь;
					
					Для Каждого ТипИзмерения Из СписокТипов Цикл 
						
						МетаданныеТипа = Метаданные.НайтиПоТипу(ТипИзмерения);
						
						Если Не МетаданныеТипа = Неопределено Тогда 
						
							ПолноеИмяМетаданного = МетаданныеТипа.ПолноеИмя();
							
							НайденнаяНастройка = ТаблицаТекущихНастроек.Найти(ПолноеИмяМетаданного, "ОбъектКонфигурации");
							
							Если Не НайденнаяНастройка = Неопределено Тогда 
								
								ДобавитьИзмерение = Истина;
								
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЦикла; 
					
					Если ДобавитьИзмерение Тогда 
						
						ЗаполнитьЗначенияСвойств(ТаблицаИзмерений.Добавить(), СтрокаКопии);
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			НовыйНаборЗаписейИзмерений = РегистрыСведений.bitmobile_КонтролируемыеИзмеренияРегистров.СоздатьНаборЗаписей();
			НовыйНаборЗаписейИзмерений.Загрузить(ТаблицаИзмерений);
			НовыйНаборЗаписейИзмерений.Записать();	
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьИЗаписатьПеречисление(ТипРеквизита, КопияРегистра, ТаблицаЗаписей)
             
	 Если Перечисления.ТипВсеСсылки().СодержитТип(ТипРеквизита) Тогда 
		 
		ПолноеИмяПеречисления = Метаданные.НайтиПоТипу(ТипРеквизита).ПолноеИмя(); 
		 
		НайденоеЗначение = КопияРегистра.Найти(ПолноеИмяПеречисления, "ИмяВКонфигурации");
		
		Вставка 					= ТаблицаЗаписей.Добавить();
		Вставка.ИмяВКонфигурации 	= ПолноеИмяПеречисления;
		
		Если Не НайденоеЗначение = Неопределено Тогда
		
			Вставка.ИмяВBitmobile 						= НайденоеЗначение.ИмяВBitmobile;
			Вставка.СоответствиеЗначенийПеречисления	= НайденоеЗначение.СоответствиеЗначенийПеречисления;
			
		КонецЕсли;	
		                                					
	КонецЕсли;
	
КонецПроцедуры
